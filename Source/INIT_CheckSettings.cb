// =====================================================================================================================================================================================================
// AVARUUS - Initial - CheckSettings
// =====================================================================================================================================================================================================

// Check if game setup file exists (and that it is not a folder).
If FileExists("Data\Settings.dat")=True And IsDirectory("Data\Settings.dat")=False Then

    // Read settings file.
    f=OpenToRead("Data\Settings.dat")
    
        musicEnabled = ReadLine(f)          // 0 or 1.
        soundEnabled = ReadLine(f)          // 0 or 1.
        musicBarAmount = ReadLine(f)        // 1-5.
        soundBarAmount = ReadLine(f)        // 1-5.
        vsyncSetting = ReadLine(f)          // 0 or 1.
        limitFrameRateIndex = ReadLine(f)   // 0-5.
        
        For i=1 To 3
            craftKeybind(i) = ReadLine(f)   // 1-4.
        Next i
        
        For i=1 To 3
            craftGamepad(i) = ReadLine(f)   // 1-4.
        Next i
        
    CloseFile f
    
    // Check and correct values if necessary.
    If musicEnabled<0 Then musicEnabled=False
    If musicEnabled>1 Then musicEnabled=True
    If soundEnabled<0 Then soundEnabled=False
    If soundEnabled>1 Then soundEnabled=True
    
    If musicBarAmount<1 Then musicBarAmount=1
    If musicBarAmount>5 Then musicBarAmount=5
    If soundBarAmount<1 Then soundBarAmount=1
    If soundBarAmount>5 Then soundBarAmount=5
    
    musicVolume = Min(musicBarAmount * 20, 100)
    soundVolume = Min(soundBarAmount * 20, 100)
    
    If vsyncSetting<0 Then vsyncSetting=0 // CPU.
    If vsyncSetting>1 Then vsyncSetting=1 // GPU.

    If limitFrameRateIndex>limitFrameRateIndexMax Then limitFrameRateIndex=limitFrameRateIndexMax
    If limitFrameRateIndex<0 Then limitFrameRateIndex=0
    
    // -----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
    // Apply limit frame rate settings.
    frameRateValue = frameRateOptions(limitFrameRateIndex)
    If frameRateValue<0 Or frameRateValue=OFF Then
        limitFrameRate=False
    Else
        limitFrameRate=True
    EndIf
    
    If limitFrameRate=True Then
        If frameRateValue<0 Or frameRateValue=OFF Then
            FrameLimit OFF
        Else
            FrameLimit frameRateValue
        EndIf
    Else
        FrameLimit OFF
    EndIf
    // -----------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
        
    For i=1 To 3
        If craftKeybind(i)<1 Or craftKeybind(i)>4 Then resetKeybinds=True // Reset if not 1-4.
    Next i
    
    If resetKeybinds=False Then
        If craftKeybind(1)=craftKeybind(2) Or craftKeybind(1)=craftKeybind(3) Or craftKeybind(2)=craftKeybind(3) Then resetKeybinds=True
    EndIf
    
    For i=1 To 3
        If craftGamepad(i)<1 Or craftGamepad(i)>4 Then resetGamepads=True // Reset if not 1-4.
    Next i
    
    If resetGamepads=False Then
        If craftGamepad(1)=craftGamepad(2) Or craftGamepad(1)=craftGamepad(3) Or craftGamepad(2)=craftGamepad(3) Then resetGamepads=True
    EndIf
    
    If resetKeybinds=True Then
        For i=1 To 3
            craftKeybind(i)=i
        Next i
    EndIf
    
    If resetGamepads=True Then
        For i=1 To 3
            craftGamepad(i)=i
        Next i
    EndIf 

Else // If settings file doesn't exist (or it is a folder) create one.

    // Create settings file.
    f=OpenToWrite("Data\Settings.dat")
    
        // Write default values in file.
        WriteLine f,1       // musicEnabled.
        WriteLine f,1       // soundEnabled.
        WriteLine f,3       // musicBarAmount.
        WriteLine f,5       // soundBarAmount.
        WriteLine f,0       // vsyncSetting.
        WriteLine f,0       // limitFrameRateIndex.
        
        For i=1 To 3
            WriteLine f,i   // craftKeybind.
        Next i
        
        For i=1 To 3
            WriteLine f,i   // craftGamepad.
        Next i
 
    CloseFile f
    
EndIf

// =====================================================================================================================================================================================================
